#! /usr/bin/env perl

# ------------------------------------------------------------------------
# Script to remove and flag GTF entries that will cause featureCounts
# to fail.
# ------------------------------------------------------------------------

use strict;
use warnings FATAL => 'all';
#use Carp::Always;

# use FindBin;
# use lib "$FindBin::Bin";
# use Xyzzy;

use constant { TRUE => 1, FALSE => 0 };

# ------------------------------------------------------------------------
# Process the command line
# ------------------------------------------------------------------------

use File::Basename;
use Getopt::Std;

our $opt_f = "gene";
our $opt_h;

sub usage {
  my $progname = basename($0);
  print STDERR "Usage: $progname [options] < annotations.gtf\n";
  print STDERR "-f FEATURE[,FEATURE,...] - features to retain\n";
  print STDERR "-h - print help\n";
  exit(@_);
}

my $stat = getopts('f:h');
if (!$stat) {
  usage(1);
}
if ($opt_h) {
  usage();
}
if (scalar(@ARGV) != 0) {
  usage(1);
}

# ------------------------------------------------------------------------

my %target_features;
foreach my $feature ( split(/,/,$opt_f) ) {
  $target_features{$feature} = TRUE;
}

# ------------------------------------------------------------------------

use constant { NO_VALUE => ";no-value;" };

sub parse_gtf_attributes {
  my ($raw_attributes) = @_;
  my $attributes = {};
  foreach my $key_val (split(/; */,$raw_attributes)) {
    my ($key,$val);
    if ( $key_val =~ /^([^ ]+) (.*)/ ) {
      ($key,$val) = ($1,$2);
      $val =~ s/^"//;
      $val =~ s/"$//;
    } else {
      ($key,$val) = ($key_val, NO_VALUE);
    }
    $attributes->{$key} = $val;
  }
  return $attributes;
}

while (<STDIN>) {
  chomp;
  if ( /^#/ ) {
    next;
  }
  my ($accession,$source,$feature,$start,$end,
      $score,$strand,$frame,$raw_attributes) = split(/\t/,$_);
  if (!$target_features{$feature}) {
    next;
  }
  if ($start >= $end) {
    my $attributes = parse_gtf_attributes($raw_attributes);
    print STDERR "Skipping non-positive length feature: ", $attributes->{gene_id},"\n";
    next;
  }
  print $_,"\n";
}
