#! /usr/bin/env perl

use strict;
use warnings;

use Getopt::Std;

our $opt_H;
our $opt_h;
our $opt_s;
our $opt_v;

sub usage {
  print STDERR "Usage: $0 [-h] [-v] REGEXP < INPUT.fastq > OUTPUT.fastq\n";

  print STDERR "-H - grep header instead of sequence\n";
  print STDERR "-h - print help\n";
  print STDERR "-s - print summary stats\n";
  print STDERR "-v - invert search\n";

  exit(@_);
}

my $stat = getopts('Hhsv');
if (!$stat) {
  usage(1);
}
if ($opt_h) {
  usage();
}
if ( $#ARGV != 0) {
  usage(1);
}

my ($raw_re) = @ARGV;
my $re = qr/$raw_re/i;

my @lines;

my $grep_line = 1;
if ($opt_H) {
  $grep_line = 0;
}

my $num_in = 0;
my $num_out = 0;


while (<STDIN>) {
  push @lines, $_;
  if ( $#lines < 3 ) { next; }
  $num_in++;
  my $found = ( $lines[$grep_line] =~ /$re/ );
  if ($found && !$opt_v || !$found && $opt_v) {
    $num_out++;
    print @lines;
  }
  @lines = ();
}

if ($opt_s) {
  print STDERR "### found $num_out of $num_in\n";
}
